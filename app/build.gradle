plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose' version '2.0.21'
    id 'kotlin-kapt'
}

android {
    namespace 'com.maegankullenda.bestbikeday'
    compileSdk 34

    defaultConfig {
        applicationId "com.maegankullenda.bestbikeday"
        minSdk 24
        targetSdk 34
        versionCode 6
        versionName "2.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "WEATHER_API_KEY", getOpenWeatherApiKey("OPENWEATHER_API_KEY_RELEASE")
        }
        debug {
            buildConfigField "String", "WEATHER_API_KEY", getOpenWeatherApiKey("OPENWEATHER_API_KEY_DEBUG")
            testCoverageEnabled true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
        buildConfig true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.1'
    }
    packaging {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    lint {
        baseline = file("lint-baseline.xml")
        abortOnError = false
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
        animationsDisabled = true
    }
}

def getOpenWeatherApiKey(String propertyName) {
    def properties = new Properties()
    def propertiesFile = rootProject.file('local.properties')
    def apiKey = "your_api_key_here"
    
    if (propertiesFile.exists()) {
        properties.load(propertiesFile.newDataInputStream())
        apiKey = properties.getProperty(propertyName) ?: apiKey
    }
    
    return "\"$apiKey\""
}

dependencies {
    def composeBom = platform('androidx.compose:compose-bom:2024.02.00')
    implementation composeBom
    androidTestImplementation composeBom

    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.7.0'
    implementation 'androidx.activity:activity-compose:1.8.2'
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.navigation:navigation-compose:2.7.7'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.squareup.moshi:moshi:1.15.0'
    implementation 'com.squareup.moshi:moshi-kotlin:1.15.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.15.0'

    // ExoPlayer dependencies
    implementation "androidx.media3:media3-exoplayer:1.2.1"
    implementation "androidx.media3:media3-ui:1.2.1"
    implementation "androidx.media3:media3-common:1.2.1"

    // Lottie Animation
    implementation "com.airbnb.android:lottie-compose:6.3.0"

    // Testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3'
    testImplementation 'io.mockk:mockk:1.13.9'
    testImplementation 'app.cash.turbine:turbine:1.0.0'
    testImplementation 'org.robolectric:robolectric:4.11.1'
    testImplementation "androidx.compose.ui:ui-test-junit4:1.6.2"
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit:1.9.0'
    
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
} 